apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

android {
    compileSdkVersion 28

    def keys = new Properties()
    file("../keys.properties").withInputStream {
        stream -> keys.load(stream)
    }

    defaultConfig {
        applicationId "com.withgoogle.experiments.unplugged"
        minSdkVersion 26
        targetSdkVersion 28
        versionCode 3
        versionName "1.0"

        buildConfigField "String", "GMAPS_API_KEY", "\"" + keys.GMAPS_API_KEY + "\""
        buildConfigField "String", "WEATHER_APP_ID", "\"" + keys.WEATHER_APP_ID + "\""
        buildConfigField "String", "PLACES_API_KEY", "\"" + keys.PLACES_API_KEY + "\""
    }

    signingConfigs {
        paperPhone {
            storeFile file(keys.storeFile)
            storePassword keys.storePassword
            keyAlias keys.keyAlias
            keyPassword keys.keyPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.paperPhone
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.1.1"

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.fragment:fragment-ktx:1.0.0'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'com.google.apis:google-api-services-tasks:v1-rev20190628-1.30.1'
    implementation 'com.google.android.gms:play-services-location:15.0.1'
    implementation 'com.google.android.libraries.places:places:2.0.0'

    implementation 'com.airbnb.android:lottie:3.0.1'

    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "com.squareup.okhttp3:okhttp:${okhttp_version}"
    implementation "com.squareup.okhttp3:logging-interceptor:${okhttp_version}"
}
